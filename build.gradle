plugins {
    id 'org.jetbrains.dokka' version '1.7.10'
    id 'org.jetbrains.kotlin.jvm' version '1.5.31'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.mailguru'
version = '0.1.0'

repositories {
    mavenCentral()
}

dependencies {

    dokkaHtmlPlugin('org.jetbrains.dokka:kotlin-as-java-plugin:1.7.10')

    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'api-core'
                description = 'Kotlin model library for usage with consumption of various APIs by mailguru.io.'
                url = 'https://github.com/mlgr-io/kotlin-api-core'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'Sascha Weyers'
                        email = 'sw@mailguru.io'
                        organization = 'mailguru UG (haftungsbeschr√§nkt)'
                        organizationUrl = 'https://mailguru.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mlgr-io/kotlin-api-core.git'
                    developerConnection = 'scm:git:ssh://github.com:mlgr-io/kotlin-api-core.git'
                    url = 'https://github.com/mlgr-io/kotlin-api-core/tree/master'
                }
            }

        }
    }
    repositories {
        mavenLocal()
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
    }
}
