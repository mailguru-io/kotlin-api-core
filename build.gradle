buildscript {
    ext {
        detektVersion = '1.21.0'
        dokkaVersion = '1.7.10'
        kotlinVersion = '1.7.10'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.21.0'
    }
}

plugins {
    id "io.gitlab.arturbosch.detekt" version "$detektVersion"
    id 'java-library'
    id 'org.jetbrains.dokka' version "$dokkaVersion"
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
    id 'org.jlleitschuh.gradle.ktlint' version '11.0.0'
}

group = 'io.mailguru'
archivesBaseName = "mailguru-api-core"
version = '0.3.0-SNAPSHOT'

apply plugin: 'com.vanniktech.maven.publish'

dependencies {

    dokkaHtmlPlugin "org.jetbrains.dokka:kotlin-as-java-plugin:$dokkaVersion"

    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    testImplementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.3'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.0'
}

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

mavenPublishing {
    publishToMavenCentral("S01")
    signAllPublications()

    pom {
        name = 'api-core'
        description = 'Kotlin model library for usage with various mailguru.io APIs.'
        url = 'https://github.com/mlgr-io/kotlin-api-core'
        licenses {
            license {
                name = 'GNU General Public License, Version 3'
                url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
            }
        }
        developers {
            developer {
                name = 'Sascha Weyers'
                email = 'sw@mailguru.io'
                organization = 'mailguru UG (haftungsbeschr√§nkt)'
                organizationUrl = 'https://mailguru.io'
            }
        }
        scm {
            connection = 'scm:git:git://github.com/mlgr-io/kotlin-api-core.git'
            developerConnection = 'scm:git:ssh://github.com:mlgr-io/kotlin-api-core.git'
            url = 'https://github.com/mlgr-io/kotlin-api-core/tree/master'
        }
    }
    // from components.java

    repositories {
        maven {
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        jvmTarget = "11"
    }
}
